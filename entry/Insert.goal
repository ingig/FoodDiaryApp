Insert
- write out 'Doing Insert'
- set string var %messageSent% = ''
- if %event% is not empty then
    - set %messageSent% = "This user request was was created on %event.CreatedAt%, take that into account when determining date property"
    - set %event% = null
- write out 'Doing message:%userInput%'
- select info from knowledge where type='macronutrients', write to %foodKnowledge%
- select * from (select * from food_entries order by id desc limit 10) order by id, write to %foodEntries%
- read llm/insertSystem.txt, load vars, to %insertSystem%
- [llm] system: %insertSystem%
    user: %userInput%
    scheme: {
        "date": datetime, 
        "dish": {
            "name": string, 
            "totalWeight": number, 
            "ingredients": [
                {"name": string, "quantity": number, "calories": number, "carbs":number, "fiber": number, "net_carb":number,"fat":number,"protein":number}
            ], 
            "totalCarbs":number, 
            "totalNetCarbs":number, 
            "totalFiber":number,
            "totalFat":number,
            "totalProtein":number, 
            "totalCalories": number
        }, 
        "emojis": string
        "missing_macronutrients_info":string|null
    }
    logger: warning
    write to %foodEntry%
- if %foodEntry.dish.name% = "unknown", call CouldNotDoEntry, call InsertEntry

InsertEntry
- insert into food_entries 
        date=%foodEntry.date%, %userInput%, dish_name=%foodEntry.dish.name%, 
        total_weight=%foodEntry.dish.totalWeight%, ingredients=%foodEntry.dish.ingredients%, 
        carbs=%foodEntry.dish.totalCarbs%, net_carbs=%foodEntry.dish.totalNetCarbs%, fiber=%foodEntry.dish.totalFiber%, 
        fat=%foodEntry.dish.totalFat%, 
        protein=%foodEntry.dish.totalProtein%, total_calories=%foodEntry.dish.totalCalories%, 
        emojis=%foodEntry.emojis%
- select everyting from today in food_entries 
        and calculate the sum of total calories as tcal, total net carbs as tcar, total proteins as tp, 
        return 1 row
- append to %output% = "%foodEntry.emojis% - Calories: %foodEntry.dish.totalCalories%
 Protein: %foodEntry.dish.totalProtein%
 Net Carbs: %foodEntry.dish.totalNetCarbs% (%foodEntry.dish.totalCarbs% / %foodEntry.dish.totalFiber%)
 Fat: %foodEntry.dish.totalFat%
 == Total today ==
   KCal: %tcal%
   Net carb: %tcar%
   Protein:%tp%
 == Total today ==
 %foodEntry.missing_macronutrients_info%"

CouldNotDoEntry
- append to %output% = %foodEntry.missing_macronutrients_info%